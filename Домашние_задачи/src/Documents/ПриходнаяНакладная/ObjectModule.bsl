

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");

КонецПроцедуры


Процедура ОбработкаПроведения(Отказ, Режим)	
	
	// регистр Взаиморасчеты 	
	
	Движения.Взаиморасчеты.Записывать = Истина;
	Движения.Взаиморасчеты.Записать();  
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
	ЭлементБлокировки.УстановитьЗначение("Поставщик", Поставщик);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();

	
	ОсталосьСписать = СуммаПоДокументу;
	
	// Чтение аванса на момент времени создания документа для выбранного поставщика
	// Авансы в регистре накопления хранятся только в неотрцательных числах.
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ВзаиморасчетыОстатки.СуммаВРубОстаток, 0) КАК АвансВРуб,
	|	ЕСТЬNULL(ВзаиморасчетыОстатки.СуммаВUSDОстаток, 0) КАК АвансВUSD,
	|	ЕСТЬNULL(ВзаиморасчетыОстатки.СуммаВEURОстаток, 0) КАК АвансВEUR
	|ИЗ
	|	РегистрНакопления.Взаиморасчеты.Остатки(
	|			&МоментВремени,
	|			Поставщик = &Поставщик
	|				И Соглашение = ЗНАЧЕНИЕ(Справочник.Соглашения.ПустаяСсылка)) КАК ВзаиморасчетыОстатки";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Поставщик", Поставщик);
	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Выборка.Следующий();
	
	АвансВРублях = Выборка.АвансВРуб;
	
	Если АвансВРублях > 0 Тогда  
		
		// регистр Взаиморасчеты Расход (Закрытие аванса)
		
		Движение = Движения.Взаиморасчеты.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Поставщик = Поставщик;
		Движение.Соглашение = Справочники.Соглашения.ПустаяСсылка();
		Движение.СуммаВРуб = Мин(АвансВРублях,ОсталосьСписать);
		
		Если Движение.СуммаВРуб = АвансВРублях Тогда
			
			//Сведение в ноль
			Движение.СуммаВUSD = Выборка.АвансВUSD;
			Движение.СуммаВEUR = Выборка.АвансВEUR;
			
		Иначе
			
			Движение.СуммаВUSD = Движение.СуммаВРуб / КурсUSD;
			Движение.СуммаВEUR = Движение.СуммаВРуб / КурсEUR;
			
		КонецЕсли;
		
		ОсталосьСписать = ОсталосьСписать - Движение.СуммаВРуб;
		
	КонецЕсли;
	
	// регистр Взаиморасчеты Расход (Запись в соглашение)
	Если ОсталосьСписать > 0 Тогда
		
		Движение = Движения.Взаиморасчеты.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Поставщик = Поставщик;	
		Движение.Соглашение = Соглашение;
		Движение.СуммаВРуб = ОсталосьСписать;
		Движение.СуммаВUSD = ОсталосьСписать / КурсUSD;
		Движение.СуммаВEUR = ОсталосьСписать / КурсEUR;
		
	КонецЕсли;
	
КонецПроцедуры

